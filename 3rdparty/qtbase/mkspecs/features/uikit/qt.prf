
qt_depends = $$resolve_depends(QT, "QT.")
!watchos:equals(TEMPLATE, app):contains(qt_depends, gui(-private)?) {

    lib_name = qios
    LIBS += -l$${lib_name}$$qtPlatformTargetSuffix()

    !no_main_wrapper {
        # The LC_MAIN load command available in iOS 6.0 and above allows dyld to
        # directly call the entrypoint instead of going through _start in crt.o.
        # Passing -e to the linker changes the entrypoint from _main to our custom
        # wrapper that calls UIApplicationMain and dispatches back to main() once
        # the application has started up and is ready to initialize QApplication.
        QMAKE_LFLAGS += -Wl,-e,_qt_main_wrapper
    }
}

load(qt)
